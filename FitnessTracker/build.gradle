buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.2'
    }
}
apply plugin: 'com.android.application'

configurations {
    preprocess
}

dependencies {
    compile files('libs/GraphView-3.1.1.jar')
    compile 'com.android.support:support-v4:20.0.0'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.squareup:otto:1.3.4'
    compile 'com.path:android-priority-jobqueue:1.1.2@aar'
    preprocess 'com.squareup.dagger:dagger-compiler:1.2.1'
    compile 'com.squareup.dagger:dagger:1.2.1'
    preprocess 'com.jakewharton:butterknife:5.1.0'
    compile 'com.jakewharton:butterknife:5.1.0'
}

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 'android-L'
    buildToolsVersion "20"

    defaultConfig {
        minSdkVersion 'L'
        targetSdkVersion 'L'
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    // make sure that lint errors don't fail the build as lint does not always work properly with the android gradle plugin
    lintOptions {
        abortOnError false
    }
}

android.applicationVariants.all { variant ->
    //Where to place the generated code
    variant.ext.set("gen_${variant.dirName}", file("${project.buildDir}/source/gen/${variant.dirName}"))

    //Do the actual preprocessor things
    variant.javaCompile.doFirst {
        File out = variant.ext.get("gen_${variant.dirName}")
        out.mkdirs()
        variant.javaCompile.options.compilerArgs += [
                '-processorpath', configurations.preprocess.getAsPath(),
                '-s', out
        ]
    }
}
